// Adapated from Scalable Prediction of Non-functional Properties in Software Product Lines.
// Scaled down by dividing numbers by 10 and rounding.
//To execute in alloy: run  show for ...


abstract IMeasurable
	footprint : integer

abstract LinkedList
	xor AbstractElement : IMeasurable 
		[ this.footprint = -12]
		ElementA : IMeasurable 
			[ this.footprint = 12]
		ElementB : IMeasurable 
			[ this.footprint = 0]
		ElementC : IMeasurable 
			[ this.footprint = 0]
	xor AbstractIterator : IMeasurable 
		[ this.footprint = 0]
		ForwardIterator : IMeasurable 
			[ this.footprint = 0]
		BackwardIterator : IMeasurable 
			[ this.footprint = 1]
	xor AbstractSort : IMeasurable  ?
		[ this.footprint = 57]
		BubbleSort : IMeasurable 
			[ this.footprint = 17]
		MergeSort : IMeasurable 
			[ this.footprint = 32]
		InsertionSort : IMeasurable 
			[ this.footprint = 0]
		QuickSort : IMeasurable 
			[ this.footprint = 22]
	print : IMeasurable  ?
		[ this.footprint = 44]
	Measurement : IMeasurable  ?
		[ AbstractSort ]
		[ this.footprint = 484]
		TCP_IP : IMeasurable
			[ this.footprint = 0]
		or SyntheticPerformanceOrMemorySize : IMeasurable
			[ this.footprint = 0]
			Performance : IMeasurable 
				[ this.footprint = 37]
			MemorySize : IMeasurable 
				[ this.footprint = 36]
	Base : IMeasurable 
		[ this.footprint = 455]
	total_footprint : integer
		[ total_footprint = sum IMeasurable.footprint]

simpleConfig : LinkedList

<< min simpleConfig.total_footprint >>

//Mandatory Features all configurations will have: Base AbstractElement AbstractIterator 
